import os

shell.executable("/bin/bash")
shell.prefix("set -euo pipefail; ")

BASE_DIR = os.path.abspath(os.getcwd())
OUTDIR = os.path.join(BASE_DIR, "results")
THREADS = 8
TMPDIR = os.path.join(BASE_DIR, "tmp")

TUMOR_SAMPLE = "CUACC327_PDX_WGS_E250051196_L01_82"
NORMAL_SAMPLE = "CU327_human_blood_WGS_E250056942_L01_61"
SAMPLES = [TUMOR_SAMPLE, NORMAL_SAMPLE]

PAIR_ID = "CUACC327_PDX_WGS_E250051196_L01_82_vs_CU327_human_blood_WGS_E250056942_L01_61"

ruleorder: apply_bqsr > bwa_mem2_align

FASTP_DIR = os.path.join(OUTDIR, "fastp")
ALIGN_DIR = os.path.join(OUTDIR, "align")
BQS_DIR = os.path.join(OUTDIR, "bqsr")
QC_DIR = os.path.join(OUTDIR, "qc")
SNV_DIR = os.path.join(OUTDIR, "snv")
SV_DIR = os.path.join(OUTDIR, "sv")
CNV_DIR = os.path.join(OUTDIR, "cnv")
MULTIQC_DIR = os.path.join(OUTDIR, "multiqc")
PAIR_DIR = os.path.join(SNV_DIR, PAIR_ID, "mutect2")
MANTA_DIR = os.path.join(SV_DIR, PAIR_ID, "manta")
CNVKIT_DIR = os.path.join(CNV_DIR, PAIR_ID, "cnvkit")
TUMOR_BQSR_BAM = os.path.join(ALIGN_DIR, TUMOR_SAMPLE + ".bqsr.bam")
TUMOR_BQSR_BAI = os.path.join(ALIGN_DIR, TUMOR_SAMPLE + ".bqsr.bam.bai")
NORMAL_BQSR_BAM = os.path.join(ALIGN_DIR, NORMAL_SAMPLE + ".bqsr.bam")
NORMAL_BQSR_BAI = os.path.join(ALIGN_DIR, NORMAL_SAMPLE + ".bqsr.bam.bai")
CNVKIT_PREFIX = TUMOR_SAMPLE + ".bqsr"

REF_DIR = os.path.join(BASE_DIR, "resources", "hg38")
REF = os.path.join(REF_DIR, "Homo_sapiens_assembly38.fasta")
REF_DICT = os.path.join(REF_DIR, "Homo_sapiens_assembly38.dict")
KNOWN_SITES_DBSNP = os.path.join(REF_DIR, "known_sites", "Homo_sapiens_assembly38.dbsnp138.vcf.gz")
KNOWN_SITES_MILLS = os.path.join(
    REF_DIR,
    "known_sites",
    "Mills_and_1000G_gold_standard.indels.hg38.vcf.gz",
)
KNOWN_SITES_PHASE1 = os.path.join(
    REF_DIR,
    "known_sites",
    "1000G_phase1.snps.high_confidence.hg38.vcf.gz",
)
GERMLINE_RESOURCE = os.path.join(REF_DIR, "somatic", "af-only-gnomad.hg38.vcf.gz")
PON = os.path.join(REF_DIR, "somatic", "1000g_pon.hg38.vcf.gz")
INTERVALS = os.path.abspath(
    os.path.join(BASE_DIR, "..", "assets", "hg38_v0_wgs_calling_regions.hg38.interval_list")
)
FASTQ_SCREEN_CONF = os.path.join(REF_DIR, "fastq_screen.conf")
KRAKEN_DB = os.path.join(REF_DIR, "kraken_db")
CNVKIT_ACCESS = os.path.join(REF_DIR, "cnvkit", "access-10kb.hg38.bed")

BWA_MEM2_BIN = "bwa-mem2.avx2"
MULTIQC_BIN = "multiqc"

KNOWN_SITES_ARGS = " ".join(
    [
        "--known-sites " + KNOWN_SITES_DBSNP,
        "--known-sites " + KNOWN_SITES_MILLS,
        "--known-sites " + KNOWN_SITES_PHASE1,
    ]
)
INTERVALS_ARG = "--intervals " + INTERVALS
GERMLINE_RESOURCE_ARG = "--germline-resource " + GERMLINE_RESOURCE
PON_ARG = "--panel-of-normals " + PON


rule all:
    input:
        expand(os.path.join(ALIGN_DIR, "{sample}.bqsr.bam"), sample=SAMPLES),
        expand(os.path.join(ALIGN_DIR, "{sample}.bqsr.bam.bai"), sample=SAMPLES),
        os.path.join(PAIR_DIR, PAIR_ID + ".mutect2.norm.vcf.gz"),
        os.path.join(PAIR_DIR, PAIR_ID + ".mutect2.norm.vcf.gz.tbi"),
        os.path.join(MANTA_DIR, PAIR_ID + ".somaticSV.vcf.gz"),
        os.path.join(MANTA_DIR, PAIR_ID + ".somaticSV.vcf.gz.tbi"),
        os.path.join(CNVKIT_DIR, PAIR_ID + ".cns"),
        os.path.join(MULTIQC_DIR, "multiqc_report.html"),


rule fastp:
    input:
        r1=os.path.join(BASE_DIR, "{sample}_1.fq.gz"),
        r2=os.path.join(BASE_DIR, "{sample}_2.fq.gz"),
    output:
        r1=os.path.join(FASTP_DIR, "{sample}.R1.trimmed.fastq.gz"),
        r2=os.path.join(FASTP_DIR, "{sample}.R2.trimmed.fastq.gz"),
        json=os.path.join(FASTP_DIR, "{sample}.fastp.json"),
        html=os.path.join(FASTP_DIR, "{sample}.fastp.html"),
    threads: THREADS
    shell:
        """
        mkdir -p {FASTP_DIR}
        fastp -w {threads} \
            -i {input.r1} -I {input.r2} \
            -o {output.r1} -O {output.r2} \
            --json {output.json} \
            --html {output.html}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule fastqc_trimmed:
    input:
        r1=os.path.join(FASTP_DIR, "{sample}.R1.trimmed.fastq.gz"),
        r2=os.path.join(FASTP_DIR, "{sample}.R2.trimmed.fastq.gz"),
    output:
        r1_zip=os.path.join(QC_DIR, "fastqc", "{sample}.R1.trimmed_fastqc.zip"),
        r1_html=os.path.join(QC_DIR, "fastqc", "{sample}.R1.trimmed_fastqc.html"),
        r2_zip=os.path.join(QC_DIR, "fastqc", "{sample}.R2.trimmed_fastqc.zip"),
        r2_html=os.path.join(QC_DIR, "fastqc", "{sample}.R2.trimmed_fastqc.html"),
    threads: THREADS
    shell:
        """
        mkdir -p {QC_DIR}/fastqc
        fastqc -t {threads} -o {QC_DIR}/fastqc {input.r1} {input.r2}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule fastq_screen:
    input:
        r1=os.path.join(FASTP_DIR, "{sample}.R1.trimmed.fastq.gz"),
        r2=os.path.join(FASTP_DIR, "{sample}.R2.trimmed.fastq.gz"),
    output:
        r1_html=os.path.join(QC_DIR, "fastq_screen", "{sample}.R1.trimmed_screen.html"),
        r1_png=os.path.join(QC_DIR, "fastq_screen", "{sample}.R1.trimmed_screen.png"),
        r1_txt=os.path.join(QC_DIR, "fastq_screen", "{sample}.R1.trimmed_screen.txt"),
        r2_html=os.path.join(QC_DIR, "fastq_screen", "{sample}.R2.trimmed_screen.html"),
        r2_png=os.path.join(QC_DIR, "fastq_screen", "{sample}.R2.trimmed_screen.png"),
        r2_txt=os.path.join(QC_DIR, "fastq_screen", "{sample}.R2.trimmed_screen.txt"),
    threads: THREADS
    shell:
        """
        mkdir -p {QC_DIR}/fastq_screen
        fastq_screen --conf {FASTQ_SCREEN_CONF} \
            --outdir {QC_DIR}/fastq_screen \
            --threads {threads} \
            --subset 1000000 \
            --aligner bowtie2 \
            --force \
            {input.r1} {input.r2}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule kraken2:
    input:
        r1=os.path.join(FASTP_DIR, "{sample}.R1.trimmed.fastq.gz"),
        r2=os.path.join(FASTP_DIR, "{sample}.R2.trimmed.fastq.gz"),
    output:
        report=os.path.join(QC_DIR, "kraken", "{sample}.trimmed.kraken_bacteria.taxa.txt"),
        krona=os.path.join(QC_DIR, "kraken", "{sample}.trimmed.kraken_bacteria.krona.html"),
    threads: THREADS
    shell:
        """
        mkdir -p {QC_DIR}/kraken
        kraken2 --db {KRAKEN_DB} \
            --threads {threads} --report {output.report} \
            --output - \
            --gzip-compressed \
            --paired {input.r1} {input.r2}
        cut -f2,3 {output.report} | \
            ktImportTaxonomy - -o {output.krona}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule bwa_mem2_align:
    input:
        r1=os.path.join(FASTP_DIR, "{sample}.R1.trimmed.fastq.gz"),
        r2=os.path.join(FASTP_DIR, "{sample}.R2.trimmed.fastq.gz"),
    output:
        bam=os.path.join(ALIGN_DIR, "{sample}.bam"),
        bai=os.path.join(ALIGN_DIR, "{sample}.bam.bai"),
    threads: THREADS
    shell:
        """
        mkdir -p {ALIGN_DIR}
        mkdir -p {TMPDIR}/bwa_{wildcards.sample}
        {BWA_MEM2_BIN} mem -M \
            -R '@RG\\tID:{wildcards.sample}\\tSM:{wildcards.sample}\\tPL:illumina\\tLB:{wildcards.sample}\\tPU:{wildcards.sample}\\tDS:wgs' \
            -t {threads} \
            {REF} \
            {input.r1} {input.r2} | \
        samblaster -M | \
        samtools sort -T {TMPDIR}/bwa_{wildcards.sample}/ \
            -@ {threads} -m 2G - \
            --write-index -o {output.bam}##idx##{output.bai}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule bqsr_table:
    input:
        bam=os.path.join(ALIGN_DIR, "{sample}.bam"),
        bai=os.path.join(ALIGN_DIR, "{sample}.bam.bai"),
    output:
        table=os.path.join(BQS_DIR, "{sample}.recal_data.grp"),
    threads: THREADS
    shell:
        """
        mkdir -p {BQS_DIR}
        gatk --java-options '-Xmx10g' BaseRecalibrator \
            --input {input.bam} \
            --reference {REF} \
            {KNOWN_SITES_ARGS} \
            {INTERVALS_ARG} \
            --output {output.table}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule apply_bqsr:
    input:
        bam=os.path.join(ALIGN_DIR, "{sample}.bam"),
        bai=os.path.join(ALIGN_DIR, "{sample}.bam.bai"),
        table=os.path.join(BQS_DIR, "{sample}.recal_data.grp"),
    output:
        bam=os.path.join(ALIGN_DIR, "{sample}.bqsr.bam"),
        bai=os.path.join(ALIGN_DIR, "{sample}.bqsr.bam.bai"),
    threads: THREADS
    shell:
        """
        gatk --java-options '-Xmx20g' ApplyBQSR \
            --reference {REF} \
            --input {input.bam} \
            --bqsr-recal-file {input.table} \
            --output {output.bam} \
            --use-jdk-inflater \
            --use-jdk-deflater
        samtools index -@ {threads} {output.bam} {output.bai}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule samtools_flagstat:
    input:
        bam=os.path.join(ALIGN_DIR, "{sample}.bqsr.bam"),
        bai=os.path.join(ALIGN_DIR, "{sample}.bqsr.bam.bai"),
    output:
        os.path.join(QC_DIR, "align", "{sample}.samtools_flagstat.txt"),
    threads: THREADS
    shell:
        """
        mkdir -p {QC_DIR}/align
        samtools flagstat {input.bam} > {output}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule mosdepth:
    input:
        bam=os.path.join(ALIGN_DIR, "{sample}.bqsr.bam"),
        bai=os.path.join(ALIGN_DIR, "{sample}.bqsr.bam.bai"),
    output:
        region=os.path.join(QC_DIR, "align", "{sample}.mosdepth.region.dist.txt"),
        summary=os.path.join(QC_DIR, "align", "{sample}.mosdepth.summary.txt"),
        regions=os.path.join(QC_DIR, "align", "{sample}.regions.bed.gz"),
        regions_csi=os.path.join(QC_DIR, "align", "{sample}.regions.bed.gz.csi"),
    threads: THREADS
    shell:
        """
        mkdir -p {QC_DIR}/align
        mosdepth -n --fast-mode --by 500 {QC_DIR}/align/{wildcards.sample} {input.bam} -t {threads}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule qualimap:
    input:
        bam=os.path.join(ALIGN_DIR, "{sample}.bqsr.bam"),
        bai=os.path.join(ALIGN_DIR, "{sample}.bqsr.bam.bai"),
    output:
        results=os.path.join(QC_DIR, "align", "{sample}_genome_results.txt"),
        html=os.path.join(QC_DIR, "align", "{sample}_qualimapReport.html"),
    threads: THREADS
    shell:
        """
        mkdir -p {QC_DIR}/align
        unset DISPLAY
        qualimap bamqc -bam {input.bam} \
            --java-mem-size=16G \
            -c -ip \
            -outdir {QC_DIR}/align/{wildcards.sample} \
            -outformat HTML \
            -nt {threads} \
            -nw 500 \
            -p NON-STRAND-SPECIFIC
        mv {QC_DIR}/align/{wildcards.sample}/genome_results.txt {output.results}
        mv {QC_DIR}/align/{wildcards.sample}/qualimapReport.html {output.html}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule mutect2_call:
    input:
        tumor_bam=TUMOR_BQSR_BAM,
        tumor_bai=TUMOR_BQSR_BAI,
        normal_bam=NORMAL_BQSR_BAM,
        normal_bai=NORMAL_BQSR_BAI,
    output:
        vcf=os.path.join(PAIR_DIR, PAIR_ID + ".mutect2.vcf.gz"),
        f1r2=os.path.join(PAIR_DIR, PAIR_ID + ".f1r2.tar.gz"),
        stats=os.path.join(PAIR_DIR, PAIR_ID + ".mutect2.vcf.gz.stats"),
    threads: THREADS
    shell:
        """
        mkdir -p {PAIR_DIR}
        gatk Mutect2 \
            --reference {REF} \
            {INTERVALS_ARG} \
            --input {input.tumor_bam} \
            --input {input.normal_bam} \
            --normal-sample {NORMAL_SAMPLE} \
            --tumor-sample {TUMOR_SAMPLE} \
            {GERMLINE_RESOURCE_ARG} \
            {PON_ARG} \
            --output {output.vcf} \
            --f1r2-tar-gz {output.f1r2} \
            --independent-mates
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule mutect2_pileup_tumor:
    input:
        bam=TUMOR_BQSR_BAM,
        bai=TUMOR_BQSR_BAI,
    output:
        os.path.join(PAIR_DIR, PAIR_ID + ".tpileup.table"),
    threads: THREADS
    shell:
        """
        mkdir -p {PAIR_DIR}
        gatk --java-options -Xmx48g GetPileupSummaries \
            -I {input.bam} \
            -V {GERMLINE_RESOURCE} \
            {INTERVALS_ARG} \
            -O {output}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule mutect2_pileup_normal:
    input:
        bam=NORMAL_BQSR_BAM,
        bai=NORMAL_BQSR_BAI,
    output:
        os.path.join(PAIR_DIR, PAIR_ID + ".npileup.table"),
    threads: THREADS
    shell:
        """
        mkdir -p {PAIR_DIR}
        gatk --java-options -Xmx48g GetPileupSummaries \
            -I {input.bam} \
            -V {GERMLINE_RESOURCE} \
            {INTERVALS_ARG} \
            -O {output}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule mutect2_contamination:
    input:
        tumor_pileup=os.path.join(PAIR_DIR, PAIR_ID + ".tpileup.table"),
        normal_pileup=os.path.join(PAIR_DIR, PAIR_ID + ".npileup.table"),
    output:
        tumor_all=os.path.join(PAIR_DIR, PAIR_ID + ".allpileups.table"),
        normal_all=os.path.join(PAIR_DIR, PAIR_ID + ".normal.allpileups.table"),
        tumor_contam=os.path.join(PAIR_DIR, PAIR_ID + ".contamination.table"),
        normal_contam=os.path.join(PAIR_DIR, PAIR_ID + ".normal.contamination.table"),
    threads: THREADS
    shell:
        """
        mkdir -p {PAIR_DIR}
        gatk GatherPileupSummaries \
            --sequence-dictionary {REF_DICT} \
            -I {input.tumor_pileup} \
            -O {output.tumor_all}

        gatk GatherPileupSummaries \
            --sequence-dictionary {REF_DICT} \
            -I {input.normal_pileup} \
            -O {output.normal_all}

        gatk CalculateContamination \
            -I {output.tumor_all} \
            --matched-normal {output.normal_all} \
            -O {output.tumor_contam}

        gatk CalculateContamination \
            -I {output.normal_all} \
            -O {output.normal_contam}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule mutect2_read_orientation:
    input:
        f1r2=os.path.join(PAIR_DIR, PAIR_ID + ".f1r2.tar.gz"),
    output:
        os.path.join(PAIR_DIR, PAIR_ID + ".read-orientation-model.tar.gz"),
    threads: THREADS
    shell:
        """
        mkdir -p {PAIR_DIR}
        gatk LearnReadOrientationModel \
            --output {output} \
            --input {input.f1r2}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule mutect2_merge_stats:
    input:
        stats=os.path.join(PAIR_DIR, PAIR_ID + ".mutect2.vcf.gz.stats"),
    output:
        os.path.join(PAIR_DIR, PAIR_ID + ".final.stats"),
    threads: THREADS
    shell:
        """
        mkdir -p {PAIR_DIR}
        gatk MergeMutectStats \
            --stats {input.stats} \
            -O {output}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule mutect2_filter:
    input:
        vcf=os.path.join(PAIR_DIR, PAIR_ID + ".mutect2.vcf.gz"),
        stats=os.path.join(PAIR_DIR, PAIR_ID + ".final.stats"),
        orientation=os.path.join(PAIR_DIR, PAIR_ID + ".read-orientation-model.tar.gz"),
        tumor_contam=os.path.join(PAIR_DIR, PAIR_ID + ".contamination.table"),
    output:
        marked=os.path.join(PAIR_DIR, PAIR_ID + ".mutect2.marked.vcf.gz"),
        marked_tbi=os.path.join(PAIR_DIR, PAIR_ID + ".mutect2.marked.vcf.gz.tbi"),
        norm=os.path.join(PAIR_DIR, PAIR_ID + ".mutect2.norm.vcf.gz"),
        norm_tbi=os.path.join(PAIR_DIR, PAIR_ID + ".mutect2.norm.vcf.gz.tbi"),
        filter_stats=os.path.join(PAIR_DIR, PAIR_ID + ".mutect2.marked.vcf.gz.filteringStats.tsv"),
    threads: THREADS
    shell:
        """
        mkdir -p {PAIR_DIR}
        gatk SortVcf -I {input.vcf} -O {PAIR_DIR}/{PAIR_ID}.mutect2.concat.vcf.gz --CREATE_INDEX
        gatk FilterMutectCalls \
            -R {REF} \
            -V {PAIR_DIR}/{PAIR_ID}.mutect2.concat.vcf.gz \
            --ob-priors {input.orientation} \
            --contamination-table {input.tumor_contam} \
            --stats {input.stats} \
            -O {output.marked}
        gatk SelectVariants \
            -R {REF} \
            --variant {output.marked} \
            --exclude-filtered \
            --output {PAIR_DIR}/{PAIR_ID}.mutect2.final.vcf.gz
        bcftools sort {PAIR_DIR}/{PAIR_ID}.mutect2.final.vcf.gz | \
            bcftools norm --threads {threads} --check-ref s -f {REF} -O v | \
            awk '{{gsub(/\\y[W|K|Y|R|S|M|B|D|H|V]\\y/,"N",$4); OFS = "\t"; print}}' | \
            sed '/^$/d' | \
            bcftools view - -Oz -o {output.norm}
        bcftools index -t {output.norm}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule manta:
    input:
        tumor_bam=TUMOR_BQSR_BAM,
        normal_bam=NORMAL_BQSR_BAM,
    output:
        vcf=os.path.join(MANTA_DIR, PAIR_ID + ".somaticSV.vcf.gz"),
        tbi=os.path.join(MANTA_DIR, PAIR_ID + ".somaticSV.vcf.gz.tbi"),
    threads: THREADS
    shell:
        """
        MANTA_CONFIG="$(command -v configManta.py)"
        MANTA_CONFIG="$(readlink -f "${{MANTA_CONFIG}}")"
        export PYTHONPATH="$(dirname "${{MANTA_CONFIG}}")${{PYTHONPATH:+:${{PYTHONPATH}}}}"
        mkdir -p {MANTA_DIR}/run
        "${{MANTA_CONFIG}}" \
            --tumorBam {input.tumor_bam} \
            --normalBam {input.normal_bam} \
            --referenceFasta {REF} \
            --runDir {MANTA_DIR}/run
        {MANTA_DIR}/run/runWorkflow.py -m local -j {threads}
        cp {MANTA_DIR}/run/results/variants/somaticSV.vcf.gz {output.vcf}
        cp {MANTA_DIR}/run/results/variants/somaticSV.vcf.gz.tbi {output.tbi}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule cnvkit_batch:
    input:
        tumor_bam=TUMOR_BQSR_BAM,
        normal_bam=NORMAL_BQSR_BAM,
    output:
        pair_cns=os.path.join(CNVKIT_DIR, PAIR_ID + ".cns"),
    threads: THREADS
    shell:
        """
        mkdir -p {CNVKIT_DIR}
        cnvkit.py batch \
            {input.tumor_bam} -n {input.normal_bam} \
            -f {REF} \
            -m wgs \
            --access {CNVKIT_ACCESS} \
            --output-reference {CNVKIT_DIR}/{PAIR_ID}.cnn \
            --output-dir {CNVKIT_DIR}
        cp {CNVKIT_DIR}/{CNVKIT_PREFIX}.cns {output.pair_cns}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """


rule multiqc:
    input:
        fastqc=expand(
            os.path.join(QC_DIR, "fastqc", "{sample}.R1.trimmed_fastqc.zip"),
            sample=SAMPLES,
        ) + expand(
            os.path.join(QC_DIR, "fastqc", "{sample}.R2.trimmed_fastqc.zip"),
            sample=SAMPLES,
        ),
        fastq_screen=expand(
            os.path.join(QC_DIR, "fastq_screen", "{sample}.R1.trimmed_screen.txt"),
            sample=SAMPLES,
        ) + expand(
            os.path.join(QC_DIR, "fastq_screen", "{sample}.R2.trimmed_screen.txt"),
            sample=SAMPLES,
        ),
        kraken=expand(
            os.path.join(QC_DIR, "kraken", "{sample}.trimmed.kraken_bacteria.taxa.txt"),
            sample=SAMPLES,
        ),
        flagstat=expand(
            os.path.join(QC_DIR, "align", "{sample}.samtools_flagstat.txt"),
            sample=SAMPLES,
        ),
        mosdepth=expand(
            os.path.join(QC_DIR, "align", "{sample}.mosdepth.summary.txt"),
            sample=SAMPLES,
        ),
        qualimap=expand(
            os.path.join(QC_DIR, "align", "{sample}_genome_results.txt"),
            sample=SAMPLES,
        ),
    output:
        os.path.join(MULTIQC_DIR, "multiqc_report.html"),
    threads: THREADS
    shell:
        """
        mkdir -p {MULTIQC_DIR}
        {MULTIQC_BIN} {OUTDIR} -o {MULTIQC_DIR}
        printf "%s %s\\n" "{rule}" "$(date -u +%Y-%m-%dT%H:%M:%SZ)" >> {OUTDIR}/rule_finish.log
        notifyme
        """
